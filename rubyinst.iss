; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{83804A08-FCEB-4D58-9D81-7FCDF9560FC0}
AppName=Ruby
AppVerName=Ruby 1.8.7
AppPublisher=Lars Christensen
DefaultDirName={pf}\Ruby/MingW
DefaultGroupName=Ruby/MingW
AllowNoIcons=yes
LicenseFile=D:/src/rubybuild/svn/svn.ruby-lang.org/repos/ruby/trunk/COPYING
InfoBeforeFile=versions.txt
OutputBaseFilename=ruby-1.8.7-p22-build-1
Compression=lzma
SolidCompression=yes
ChangesAssociations=yes

[Registry]
Root: HKCR; Subkey: ".rb"; ValueType: string; ValueName: ""; ValueData: "RubyScript"; Flags: uninsdeletevalue; Tasks: assocrb
Root: HKCR; Subkey: "RubyScript"; ValueType: string; ValueName: ""; ValueData: "Ruby Interpreter"; Flags: uninsdeletevalue; Tasks: assocrb
Root: HKCR; Subkey: "RubyScript\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\ruby.exe"" ""%1"" %*"; Tasks: assocrb
Root: HKCR; Subkey: ".rbw"; ValueType: string; ValueName: ""; ValueData: "RubyScriptW"; Flags: uninsdeletevalue; Tasks: assocrbw
Root: HKCR; Subkey: "RubyScriptW"; ValueType: string; ValueName: ""; ValueData: "Ruby Interpreter"; Flags: uninsdeletevalue; Tasks: assocrbw
Root: HKCR; Subkey: "RubyScriptW\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\bin\rubyw.exe"" ""%1"" %*"; Tasks: assocrbw

[Tasks]
Name: path; Description: Add Ruby to PATH
Name: assocrb; Description: Associate .rb files with ruby.exe
Name: assocrbw; Description: Associate .rbw files with rubyw.exe
Name: pathext; Description: Run scripts from command line without .rb extension (modify PATHEXT)

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "tmpinstall\*"; DestDir: "{app}"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Code]
procedure UpdatePathExt();
var
  PathExt: String;
begin
  if IsTaskSelected('pathext') then begin
    RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATHEXT', PathExt);
    StringChangeEx(PathExt, ';.RBW', '', True);
    StringChangeEx(PathExt, ';.RB', '', True);
    PathExt := PathExt + ';.RB;.RBW';
    RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATHEXT', PathExt);
  end
  
  if IsTaskSelected('path') then begin
    RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', PathExt);
    StringChangeEx(PathExt, ';' + ExpandConstant('{app}') + '\bin', '', True);
    PathExt := PathExt + ';' + ExpandConstant('{app}') + '\bin';
    RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', PathExt);
  end
end;

procedure UninstallPathExt();
var
  PathExt: String;
begin
  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATHEXT', PathExt);
  StringChangeEx(PathExt, ';.RB', '', True);
  StringChangeEx(PathExt, ';.RBW', '', True);
  RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATHEXT', PathExt);
  
  RegQueryStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', PathExt);
  StringChangeEx(PathExt, ';' + ExpandConstant('{app}') + '\bin', '', True);
  RegWriteStringValue(HKEY_LOCAL_MACHINE, 'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', PathExt);
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  if CurUninstallStep = usPostUninstall then
    UninstallPathExt();
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssPostInstall then
    UpdatePathExt();
end;

